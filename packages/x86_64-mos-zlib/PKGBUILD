# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_target=x86_64-mos
_prefix=/opt/$_target

_pkgbase=zlib
pkgname=($_target-zlib $_target-minizip)
epoch=1
pkgver=1.3.1
pkgrel=33
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=(any)
license=(Zlib)
url="https://www.zlib.net/"
makedepends=($_target-gcc)
depends=($_target-mlibc)
options=(
  staticlibs # needed by binutils testsuite
  debug
)
source=(https://github.com/madler/zlib/releases/download/v$pkgver/$_pkgbase-$pkgver.tar.xz
  zlib-soname.patch)
sha512sums=('1e8e70b362d64a233591906a1f50b59001db04ca14aaffad522198b04680be501736e7d536b4191e2f99767e7001ca486cd802362cca2be05d5d409b83ea732d'
            '5274b9acae5d6f10b2ae75b28030232adc8b936562ba7f20d8f71705943b7330c16decb36d7f95ce15deb7deee4e0606695bd2505983a1847931c2f2ee6e1d1d')
b2sums=('42d109223801a493de6d52e7343403d7fc3234a6ca816425fe41ac9c18019b01b93841acd28a235e99f2256a6a17f93624e96b2ddb58d588c8190a6bedb82910'
        'c3a20aa32bfaf1326f11c907b7b0d87a0f7b6d15f1677f9e64b02f53178677faeccfad19643eac9aa6bcd5f1bf12b004c0a92d234c99667e153452a979038e5f')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA') # Mark Adler <madler@alumni.caltech.edu>

prepare() {
  cd $_pkgbase-$pkgver/contrib/minizip
  cp Makefile Makefile.orig
  cp ../README.contrib readme.txt
  autoreconf -fiv
  sed -i 's/| fiwix*/| fiwix* | mos*/' config.sub
  sed -i 's/defined(MINIZIP_FOPEN_NO_64)/defined(MINIZIP_FOPEN_NO_64) || defined(__mos__)/' ioapi.c # fopen64 is not a standard function
  cd $srcdir/$_pkgbase-$pkgver/
  pwd
  patch -s -Np1 <"${srcdir}/zlib-soname.patch"
}

build() {
  cd $_pkgbase-$pkgver

  CFLAGS+=" -ffat-lto-objects"
  CHOST=$_target ./configure --prefix=/$_prefix
  make

  cd contrib/minizip
  CHOST=$_target ./configure --host=$_target --prefix=/$_prefix --enable-static=no
  make
}

package_x86_64-mos-zlib() {
  provides=(libz.so)

  make install DESTDIR="$pkgdir" -C $_pkgbase-$pkgver
  install -D -m644 $_pkgbase-$pkgver/LICENSE -t "$pkgdir/$_prefix/share/licenses/zlib/"
}

package_x86_64-mos-minizip() {
  pkgdesc='Mini zip and unzip based on zlib'
  depends+=("$_target-zlib")

  make install DESTDIR="$pkgdir" -C $_pkgbase-$pkgver/contrib/minizip
  install -D -m644 $_pkgbase-$pkgver/LICENSE -t "$pkgdir/$_prefix/share/licenses/minizip/"

  # https://github.com/madler/zlib/pull/229
  rm "$pkgdir/$_prefix/include/minizip/crypt.h"
}

# vim: ts=2 sw=2 et:
