_target=riscv64-mos
_prefix=/opt/$_target
_pkgname=tomlplusplus
pkgname=$_target-tomlplusplus-headers
pkgver=3.4.0
pkgrel=2
pkgdesc="Header-only TOML config file parser and serializer for C++17"
arch=(any)
url="https://marzer.github.io/tomlplusplus/"
license=(MIT)
depends=()
makedepends=(cmake meson riscv64-mos-gcc)
source=(
  $_pkgname-$pkgver.tar.gz::https://github.com/marzer/$_pkgname/archive/refs/tags/v$pkgver.tar.gz
  mos-riscv64.cross-file
)
sha512sums=('c227fc8147c9459b29ad24002aaf6ab2c42fac22ea04c1c52b283a0172581ccd4527b33c1931e0ef0d1db6b6a53f9e9882c6d4231c7f3494cf070d0220741aa5'
            '20e41ed69a6d2891084d3a3c8ed5f69a90eeb9441e87bc85cc030a595e2ef242b565103d0827df2820420d4b5fb50aa06df21f399af6b14c7d0b6b2743cbd6b9')
b2sums=('9495ccd78707ced11744eab7c1c0bf0c0c28e283d186195bb48d1059bae7eb1a874bc964b0fc45210fd73ffd7485ecf3e1159da227d0e1c8ff249e79c08eecf0'
        '1be22b64a8c9c1a48b18b82f63bdae11af8f17fb94aa4867af250c0b747a2b075748e9d127d0df8064b1663627e2551f34a7fe9217226907c205a629f72f495c')

build() {
  meson --cross-file="$srcdir/mos-riscv64.cross-file" $_pkgname-$pkgver build -Dcompile_library=false --prefix=$_prefix
  ninja -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $_pkgname-$pkgver/LICENSE -t "$pkgdir/$_prefix/share/licenses/$pkgname/"
}
