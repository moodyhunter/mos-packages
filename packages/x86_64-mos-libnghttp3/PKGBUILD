# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Christian Hesse <eworm@archlinux.org>

_target=x86_64-mos
pkgname=$_target-libnghttp3
pkgver=1.4.0
pkgrel=5
pkgdesc="HTTP/3 library written in C"
url='https://github.com/ngtcp2/nghttp3'
arch=('any')
license=('MIT')
depends=($_target-mlibc)
makedepends=("$_target-gcc")
provides=('libnghttp3.so')
source=("https://github.com/ngtcp2/nghttp3/releases/download/v${pkgver}/nghttp3-${pkgver}.tar.xz")
sha256sums=('c87b27ef33982a3b831dd349f4a75c55bd4c22a8ec0890095b84b54009df9d6a')

prepare() {
  cd nghttp3-${pkgver}
  autoreconf -i
  sed -i 's/| minix/| minix* | mos/' config.sub
}

build() {
  cd nghttp3-${pkgver}
  ./configure \
    --prefix=/opt/${_target} \
    --host=${_target}
  make
}

package() {
  cd nghttp3-${pkgver}/lib
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING -t "${pkgdir}/opt/$_target/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
