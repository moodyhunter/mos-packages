# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Christian Hesse <eworm@archlinux.org>

_target=x86_64-mos
pkgname=$_target-libnghttp3
pkgver=1.7.0
pkgrel=5
pkgdesc="HTTP/3 library written in C"
url='https://github.com/ngtcp2/nghttp3'
arch=('any')
license=('MIT')
depends=($_target-mlibc)
makedepends=("$_target-gcc")
provides=('libnghttp3.so')
source=("https://github.com/ngtcp2/nghttp3/releases/download/v${pkgver}/nghttp3-${pkgver}.tar.xz")
sha256sums=('b4eb6bceb99293d9a9df2031c1aad166af3d57b3e33655aca0699397b6f0d751')

prepare() {
  cd nghttp3-${pkgver}
  autoreconf -i
  sed -i 's/| minix/| minix* | mos/' config.sub
}

build() {
  cd nghttp3-${pkgver}
  ./configure \
    --prefix=/opt/${_target} \
    --host=${_target}
  make
}

package() {
  cd nghttp3-${pkgver}/lib
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING -t "${pkgdir}/opt/$_target/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
