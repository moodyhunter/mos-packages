# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>
# Contributor: Louis R. Marascio <lrm@fitnr.com>
# Contributor: Cody Maloney <cmaloney@theoreticalchaos.com>
# Contributor: acxz <akashpatel2008 at yahoo dot com>

_target=riscv64-mos
_prefix=/opt/$_target
_pkgname=gtest
pkgname=$_target-gtest
pkgver=1.14.0
pkgrel=10
pkgdesc='Google Test - C++ testing utility'
url='https://github.com/google/googletest'
arch=('any')
license=('BSD')
depends=("$_target-mlibc" "$_target-bash")
makedepends=('python' 'cmake' "$_target-gcc" 'sh' 'mos-sdk')
optdepends=('python: gmock generator')
_srcname=googletest-${pkgver}
source=(${_srcname}.tar.gz::https://github.com/google/googletest/archive/v${pkgver}.tar.gz
        gtest-version.patch)
sha512sums=('765c326ccc1b87a01027385e69238266e356361cd4ee3e18e3c9d137a5d11fa5d657c164d02dd1be8fe693c8e10f2b580588dbfa57d27f070e2750f50d3e662c'
            '8d426a1759a9931db5f8431d83d163fd1bc986be27f85c24222d69a18224451c5a745a29f783e55c83af1882bd0bbc2504f91668c38d6622810bb65888897799')

prepare() {
  cd ${_srcname}
  sed "s|__GOOGLETEST_VERSION__|${pkgver}|g" -i ../gtest-version.patch
  patch -Np1 -i ../gtest-version.patch
}

build() {
 cmake -H${_srcname} -Bbuild \
    -DCMAKE_TOOLCHAIN_FILE=/opt/${_target}/toolchain.cmake \
    -DCMAKE_INSTALL_PREFIX=$_prefix \
    -DBUILD_SHARED_LIBS=ON \
    -Dgtest_build_tests=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install

  # Shouldn't be present
  find "${pkgdir}" -name '*.pump' -printf 'Removing %P\n' -delete

  cd ${_srcname}
  install -Dm 644 LICENSE -t "${pkgdir}/$_prefix/share/licenses/${_pkgname}"
  install -Dm 644 README.md CONTRIBUTORS -t "${pkgdir}/$_prefix/share/doc/${_pkgname}"

  cd googletest
  install -Dm 644 cmake/* -t "${pkgdir}/$_prefix/src/googletest/cmake"
  install -Dm 644 src/* -t "${pkgdir}/$_prefix/src/googletest/src"
  install -Dm 644 CMakeLists.txt -t "${pkgdir}/$_prefix/src/googletest"

  cd ../googlemock
  install -Dm 644 cmake/* -t "${pkgdir}/$_prefix/src/gmock/cmake"
  install -Dm 644 src/* -t "${pkgdir}/$_prefix/src/gmock/src"
  install -Dm 644 CMakeLists.txt -t "${pkgdir}/$_prefix/src/gmock"

  sed -i 's|src/||' "${pkgdir}/$_prefix/src/gmock/src/gmock-all.cc"

  python -m compileall -d /$_prefix/share/gmock "${pkgdir}/$_prefix/share/gmock"
  python -O -m compileall -d /$_prefix/share/gmock "${pkgdir}/$_prefix/share/gmock"
}

# vim: ts=2 sw=2 et:
