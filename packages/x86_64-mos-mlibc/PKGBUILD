# Maintainer: Moody <mooodyhunter@outlook.com>

_target=x86_64-mos
_mos_arch=x86_64
_pkgbase=$_target-mlibc

pkgname=$_pkgbase
pkgver=3847.94877394
pkgrel=2
pkgdesc="mlibc for the $_target toolchain"
arch=('any')
url="https://github.com/moodyhunter/mlibc-mos"
license=('MIT')
conflicts=($_target-mlibc-headers)
groups=($_target-toolchain)
depends=($_target-mos-api-headers $_target-linux-api-headers)
makedepends=(git gcc meson $_target-gcc)
source=("mlibc::git+https://github.com/moodyhunter/mlibc-mos.git")
sha512sums=('SKIP')
options+=(staticlibs)

pkgver() {
    cd mlibc
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd mlibc
    meson setup build \
        --prefix=$pkgdir/opt/$_target \
        --cross-file=ci/mos-$_mos_arch.cross-file \
        -Ddefault_library=both \
        -Dlinux_kernel_headers="/opt/$_target/include"
    sed -i 's/__STDC_HOSTED__/0/g' ./subprojects/frigg/include/frg/logging.hpp
}

build() {
    cd mlibc/build
    ninja
}

package() {
    cd mlibc/build
    ninja install
}
