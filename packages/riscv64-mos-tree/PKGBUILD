# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
_target=riscv64-mos
_prefix=/opt/$_target

pkgname=$_target-tree
pkgver=2.1.1
pkgrel=36
pkgdesc="A directory listing program displaying a depth indented list of files"
arch=(any)
url="https://gitlab.com/OldManProgrammer/unix-tree"
license=('GPL')
depends=($_target-mlibc)
makedepends=($_target-gcc)
source=("https://gitlab.com/OldManProgrammer/unix-tree/-/archive/${pkgver}/unix-tree-${pkgver}.tar.gz"
	"0001-riscv64-mos-target.patch")
sha512sums=('997d20c5508d3eba026e9d621a4da2b3b0bf111d272bb7d705a9a0819d430887061e234f0a00ac3102c43413ef716c1371ee0620b8460fbd523d4a3790940a29'
            'b1f90c464874e4e05259803b83dfc53b98239487c89e0de7cd611c58a0f7a8eca44546e38a690d3d3e8ef4b2355bd836dfba89c4392b00a401ee30d15ca2bb3d')

prepare() {
  cd "unix-tree-${pkgver}"
  patch -Np1 < $srcdir/0001-riscv64-mos-target.patch
}

build() {
  cd "unix-tree-${pkgver}"
  make
}

package() {
  cd "unix-tree-${pkgver}"
  make PREFIX="${pkgdir}${_prefix}" MANDIR="${pkgdir}${_prefix}/share/man" install
}
