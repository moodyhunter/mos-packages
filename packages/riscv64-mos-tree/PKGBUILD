# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
_target=riscv64-mos
_prefix=/opt/$_target

pkgname=$_target-tree
pkgver=2.2.1
pkgrel=3
pkgdesc="A directory listing program displaying a depth indented list of files"
arch=(any)
url="https://gitlab.com/OldManProgrammer/unix-tree"
license=('GPL')
depends=($_target-mlibc)
makedepends=($_target-gcc)
source=("https://gitlab.com/OldManProgrammer/unix-tree/-/archive/${pkgver}/unix-tree-${pkgver}.tar.gz"
	"0001-riscv64-mos-target.patch")
sha512sums=('e7f514c2c12bc409d28c83a2c359bf463f8c624b6fc78cb847e0849925dfca746a9dac9a9e55b721972c474a33dba99a069f69694eaa08c12f4ce77db6d49953'
            'b1f90c464874e4e05259803b83dfc53b98239487c89e0de7cd611c58a0f7a8eca44546e38a690d3d3e8ef4b2355bd836dfba89c4392b00a401ee30d15ca2bb3d')

prepare() {
  cd "unix-tree-${pkgver}"
  patch -Np1 < $srcdir/0001-riscv64-mos-target.patch
}

build() {
  cd "unix-tree-${pkgver}"
  make
}

package() {
  cd "unix-tree-${pkgver}"
  make PREFIX="${pkgdir}${_prefix}" MANDIR="${pkgdir}${_prefix}/share/man" install
}
