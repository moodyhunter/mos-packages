# Maintainer: David Runge <dvzrv@archlinux.org>
_target=x86_64-mos
_prefix=/opt/$_target/
pkgname=$_target-nlohmann-json
pkgver=3.12.0
pkgrel=4
# tests are tracked in a separate repo
# https://github.com/nlohmann/json_test_data
_test_pkgver=3.1.0
pkgdesc="JSON for Modern C++"
arch=(any)
url="https://github.com/nlohmann/json"
license=(MIT)
makedepends=(
  mos-sdk
  cmake
  git
)
source=(
  $pkgname::git+$url.git#tag=v$pkgver
)
sha512sums=('9980bd1a6b39a02a87495564f35e9d6a1d9f359d654c3009040da62ba6ad33747767703c680fe5c7e269887939c0f9a7335b41c7459983e10069925eb9f72347')
b2sums=('06e798f1ae980cf7644ff8712bb3aaff00d32803d5bf47f84b754f291a09ffc3918a321d951e736c0c68329eaa1a7a3f082b5db2a01d949ae7ec4d42f22a4422')

prepare() {
  mkdir -vp build-test/
}

build() {
  local cmake_options=(
    -D CMAKE_TOOLCHAIN_FILE=/$_prefix/toolchain.cmake
    -B build
    -D BUILD_TESTING=ON
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=$_prefix/
    -D CMAKE_INSTALL_LIBDIR=$_prefix/lib/
    -D JSON_BuildTests=OFF
    -D JSON_MultipleHeaders=ON
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/README.md -t "$pkgdir/$_prefix/share/doc/$pkgname/"
  install -vDm 644 $pkgname/LICENSE.MIT -t "$pkgdir/$_prefix/share/licenses/$pkgname/"
}
