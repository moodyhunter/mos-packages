# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_target=x86_64-mos
_prefix=/opt/$_target

_pkgbase='protobuf'
_pkgname='protobuf'
pkgname="$_target-protobuf"
pkgver=25.3
pkgrel=21
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=(any)
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=(
  "$_target-mlibc"
  "$_target-zlib"
  "$_target-abseil-cpp"
)
makedepends=(
  'cmake'
  "$_target-gtest"
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  "protobuf" # host protobuf
  'mos-sdk' # for cmake MOS system specific files
)
source=(https://github.com/protocolbuffers/protobuf/archive/v$pkgver/$_pkgname-$pkgver.tar.gz
        https://github.com/protocolbuffers/protobuf/commit/2e62ef1e.patch
        soversion.patch
        $_pkgbase-21.12-pep517.patch)  # let's not call setup.py like a script and just build...
sha512sums=('1f73e237c919082e5423ae9e2ea8813dccf672c059051d1531fe89ffaa45872d3cf3052b8c3af26f674296ec17d7dc861c67b8f0834ed80261ce4a6a14ed7115'
            'f42d9bd702abe2c7fc4dcb07d050376287ac60b0b7e2fde0d7a9e9df24a620866bee5fd7de2e3b216095376de47e1fe7443cca74c9a9e85c1a0bc42e8973a280'
            'f0813a415cff5639e4709400f15b0c5565294e7907ae164e620b76258734c643115d8e5170bf0e4aee264c347fb7e01ac4be60d19be2a91c0ce9c561dad8c8e9'
            'a297e74ee4f807b3fad7da7d0de6dd9647963521be66cd2a2370343f5bd191cbb38759157ac0cdb161a5893a30a10520a5098e88a292c800859af33db5cf7a41')

prepare() {
  patch -d $_pkgbase-$pkgver -p1 < $_pkgname-21.12-pep517.patch
  patch -d $_pkgbase-$pkgver -p1 < 2e62ef1e.patch # Fix cmake config compatibility mode
  patch -d $_pkgbase-$pkgver -p1 < soversion.patch # Restore soversion
  sed -e 's|c++14|c++17|' -i $_pkgbase-$pkgver/python/setup.py
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_TOOLCHAIN_FILE=/opt/${_target}/toolchain.cmake
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=$_prefix
    -D protobuf_BUILD_SHARED_LIBS=ON
    -D protobuf_USE_EXTERNAL_GTEST=ON
    -D protobuf_ABSL_PROVIDER=package
    -D protobuf_BUILD_TESTS=OFF
    -S $_pkgbase-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose

  cd $_pkgbase-$pkgver/python
  LDFLAGS+=" -L${srcdir}/build" \
  PROTOC="/usr/bin/protoc" \
  python -m build --wheel --no-isolation
}

package() {
  provides=('libprotoc.so' 'libprotobuf.so' 'libprotobuf-lite.so')

  DESTDIR="$pkgdir" cmake --install build

  cd $_pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir$_prefix/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir$_prefix/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}$_prefix/share/vim/vimfiles/syntax"
}
