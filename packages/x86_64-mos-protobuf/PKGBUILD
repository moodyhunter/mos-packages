# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_target=x86_64-mos
_prefix=/opt/$_target

_pkgbase='protobuf'
_pkgname='protobuf'
pkgname="$_target-protobuf"
pkgver=31.0
pkgrel=7
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=(any)
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=(
  "$_target-mlibc"
  "$_target-zlib"
  "$_target-abseil-cpp"
)
makedepends=(
  'cmake'
  "$_target-gcc"
  "$_target-gtest"
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  "protobuf" # host protobuf
  'mos-sdk' # for cmake MOS system specific files
)
source=(https://github.com/protocolbuffers/protobuf/archive/v$pkgver/$_pkgname-$pkgver.tar.gz
        https://github.com/protocolbuffers/protobuf/commit/2e62ef1e.patch)  # let's not call setup.py like a script and just build...
sha512sums=('e756ffc2a682e06db9026166ac9b45103b1afbb97fb8a008f16cce542f6c5110eb4168ffbc0c2eb9df5e43d620ccf2a8a7547caf24799eb20911257599a0a35e'
            '18bc71031bbcbc3810a9985fa670465040f06a6c104ab8079b56bdfc499bb6cec40805a0cefd455031142490a576dc60aa8000523877ac0353b93558e9beabbd')

prepare() {
  patch -d $_pkgbase-$pkgver -p1 < 2e62ef1e.patch # Fix cmake config compatibility mode
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_TOOLCHAIN_FILE=/opt/${_target}/toolchain.cmake
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=$_prefix
    -D protobuf_BUILD_SHARED_LIBS=ON
    -D protobuf_USE_EXTERNAL_GTEST=ON
    -D protobuf_ABSL_PROVIDER=package
    -D protobuf_BUILD_TESTS=OFF
    -S $_pkgbase-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose

  # MOS: skip python build for now
  # cd $_pkgbase-$pkgver/python
  # LDFLAGS+=" -L${srcdir}/build" \
  # PROTOC="/usr/bin/protoc" \
  # python -m build --wheel --no-isolation
}

package() {
  provides=('libprotoc.so' 'libprotobuf.so' 'libprotobuf-lite.so')

  DESTDIR="$pkgdir" cmake --install build

  cd $_pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir$_prefix/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir$_prefix/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}$_prefix/share/vim/vimfiles/syntax"
}
