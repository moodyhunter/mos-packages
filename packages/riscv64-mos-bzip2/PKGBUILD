# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd <jvinet@zeroflux.org>

_target=riscv64-mos
_pkgbase=bzip2
pkgname=$_target-bzip2
pkgver=1.0.8
pkgrel=28
pkgdesc="A high-quality data compression program"
arch=('any')
license=('BSD')
url="https://sourceware.org/bzip2/"
depends=("$_target-mlibc")
makedepends=("$_target-gcc")
provides=('libbz2.so')
source=(https://sourceware.org/pub/bzip2/$_pkgbase-$pkgver.tar.gz
        bzip2.pc)
sha256sums=('ab5a03176ee106d3f0fa90e381da478ddae405918153cca248e682cd0c4a2269'
            'eca9d8cd6376df1fb5442667c603032023fb21d8d183684550da0b96ade39654')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

prepare() {
  cd $_pkgbase-$pkgver
  cp ../bzip2.pc bzip2.pc
  sed "s|@VERSION@|$pkgver|" -i bzip2.pc
}

build() {
  cd $_pkgbase-$pkgver

  make -f Makefile-libbz2_so CC="$_target-gcc $CFLAGS $CPPFLAGS $LDFLAGS"
  make bzip2 bzip2recover CC="$_target-gcc $CFLAGS $CPPFLAGS $LDFLAGS"
}

package() {
  cd $_pkgbase-$pkgver

  install -dm755 "$pkgdir"/opt/$_target/{bin,lib,include,share/man/man1}

  install -m755 bzip2-shared "$pkgdir"/opt/$_target/bin/bzip2
  install -m755 bzip2recover bzdiff bzgrep bzmore "$pkgdir"/opt/$_target/bin
  ln -sf bzip2 "$pkgdir"/opt/$_target/bin/bunzip2
  ln -sf bzip2 "$pkgdir"/opt/$_target/bin/bzcat

  cp -a libbz2.so* "$pkgdir"/opt/$_target/lib
  ln -s libbz2.so.$pkgver "$pkgdir"/opt/$_target/lib/libbz2.so
  ln -s libbz2.so.$pkgver "$pkgdir"/opt/$_target/lib/libbz2.so.1 # For compatibility with some other distros

  install -m644 bzlib.h "$pkgdir"/opt/$_target/include/

  install -m644 bzip2.1 "$pkgdir"/opt/$_target/share/man/man1/
  ln -sf bzip2.1 "$pkgdir"/opt/$_target/share/man/man1/bunzip2.1
  ln -sf bzip2.1 "$pkgdir"/opt/$_target/share/man/man1/bzcat.1
  ln -sf bzip2.1 "$pkgdir"/opt/$_target/share/man/man1/bzip2recover.1

  install -Dm644 bzip2.pc -t "$pkgdir"/opt/$_target/lib/pkgconfig
  install -Dm644 LICENSE "$pkgdir"/opt/$_target/share/licenses/${_pkgbase}/LICENSE
}
