# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Martin Schmölzer <mschmoelzer@gmail.com>

_target=riscv64-elf
pkgname=$_target-gdb
pkgver=16.3
pkgrel=9
pkgdesc='The GNU Debugger for the RISCV64 (bare-metal) target'
arch=(x86_64)
url='https://www.gnu.org/software/gdb/'
license=(GPL3)
depends=(xz ncurses expat python guile gdb-common mpfr libelf source-highlight)
makedepends=(boost)
options=(!emptydirs)
groups=($_target-toolchain)
source=(https://ftp.gnu.org/gnu/gdb/gdb-$pkgver.tar.xz)
sha512sums=('fffd6689c3405466a179670b04720dc825e4f210a761f63dd2b33027432f8cd5d1c059c431a5ec9e165eedd1901220b5329d73c522f9a444788888c731b29e9c')

build() {
    cd gdb-$pkgver

    mkdir -p build && cd build
    ../configure \
        --target=$_target \
        --prefix=/usr \
        --enable-languages=c,c++ \
        --enable-multilib \
        --enable-interwork \
        --with-system-readline \
        --disable-nls \
        --enable-source-highlight \
        --with-python=/usr/bin/python \
        --with-system-gdbinit=/etc/gdb/gdbinit

    make
}

package() {
    cd gdb-$pkgver/build

    make -C gdb DESTDIR="$pkgdir" install

    # Following files conflict with 'gdb'/'gdb-common' packages
    rm -r "$pkgdir"/usr/include/gdb/
    rm -r "$pkgdir"/usr/share/gdb/
    rm -r "$pkgdir"/usr/share/info/
    rm -r "$pkgdir"/usr/share/man/man5/
}
