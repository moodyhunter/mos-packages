# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Martin Schmölzer <mschmoelzer@gmail.com>

_target=riscv64-elf
pkgname=$_target-gdb
pkgver=15.2
pkgrel=1
pkgdesc='The GNU Debugger for the RISCV64 (bare-metal) target'
arch=(x86_64)
url='https://www.gnu.org/software/gdb/'
license=(GPL3)
depends=(xz ncurses expat python guile gdb-common mpfr libelf source-highlight)
makedepends=(boost)
options=(!emptydirs)
groups=($_target-toolchain)
source=(https://ftp.gnu.org/gnu/gdb/gdb-$pkgver.tar.xz)
sha512sums=('624007deceb5b15ba89c0725883d1a699fa46714ef30887f3d0165e17c5d65d634671740a135aa69e437d916218abb08cfa2a38ed309ff19d48f51da56b2a8ba')

build() {
    cd gdb-$pkgver

    mkdir -p build && cd build
    ../configure \
        --target=$_target \
        --prefix=/usr \
        --enable-languages=c,c++ \
        --enable-multilib \
        --enable-interwork \
        --with-system-readline \
        --disable-nls \
        --enable-source-highlight \
        --with-python=/usr/bin/python \
        --with-system-gdbinit=/etc/gdb/gdbinit

    make
}

package() {
    cd gdb-$pkgver/build

    make -C gdb DESTDIR="$pkgdir" install

    # Following files conflict with 'gdb'/'gdb-common' packages
    rm -r "$pkgdir"/usr/include/gdb/
    rm -r "$pkgdir"/usr/share/gdb/
    rm -r "$pkgdir"/usr/share/info/
    rm -r "$pkgdir"/usr/share/man/man5/
}
