# Maintainer:  David Runge <dvzrv@archlinux.org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_target=x86_64-mos
_prefix=/opt/${_target}

_pkgname=ncurses
pkgname=$_target-ncurses
pkgver=6.4_20231217
pkgrel=5
pkgdesc='System V Release 4.0 curses emulation library'
arch=(x86_64)
url='https://invisible-island.net/ncurses/ncurses.html'
license=(MIT)
depends=($_target-mlibc)
makedepends=(autoconf-archive $_target-gcc)
optdepends=('bash: for ncursesw6-config')
provides=(libncurses++w.so libformw.so libmenuw.so libpanelw.so libncursesw.so)
replaces=(alacritty-terminfo)
options+=(!strip debug)
source=(
  https://invisible-mirror.net/archives/$_pkgname/current/$_pkgname-${pkgver/_/-}.tgz
  $_pkgname-6.3-libs.patch
  $_pkgname-6.3-pkgconfig.patch
  mos.patch
)
sha512sums=('3320a867a783046c8394e133ebbe5724a81892e60413419649accc5923be01b7e06626323224f0ea75aa4f4adf30947f5c147a913fdc3f34d5ae36c3f48adfce'
            'adb02b838c40f1e58a1b31c26d5cd0f2a1c43f3b6d68e839981764c0f6c905a9eb51dd36ff018628fdeb20747cc7467727d57135408ab4848259384077a52b28'
            '2d2c0ec3c880e638ab4aa3dbff5e28e4cd233153e24816bd87e077f848aa3edd5114cd0f2a7f6e8869dd1861a2746e512886c18264ff1676927dcc320c5ef958'
            'a251dde425c7785b5acc4a3a5c09a4a048e09618179e3c2dc7c615ca108d5fb22b3f7d5282d6237442105cf2b964830643094fff2d2ad9c34b261977e5ab054f')
b2sums=('6e0829240518fef2b050a18f2c89f4fc945edf317351d8093112835328cd5b305fb65ab7664235dcda6eea125ab236823b25137835b537f309521a471967c69d'
        '31bb10e82dd018a75e57252052650d9f0f5eb5e7e887118c2ea40032b11f59ec6aa4d9bae804c615cbecdf3382f3434e0c9e9e8440fdefe66a507be020b8965c'
        'fb6cf606cf3db7f6b306272696a63bce83d52cfa91e850f9a7bdb9d3d8455a26943529a9cf79731dddc7f763c27211a9afab9c4c31dbb6d12fd720eb390eb0a3'
        'ea19c26843add8664399d86215ad48a5500ce3185b93c1b17fe67aa53e14301cf47660e3f3f68f5d7a7d3cd73c191f07d837a4bfcbc0ace28af22faf9b6e90ef')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03')  # Thomas Dickey <dickey@invisible-island.net>

prepare() {
  # do not link against test libraries
  patch -Np1 -d $_pkgname-${pkgver/_/-} -i ../$_pkgname-6.3-libs.patch
  # do not leak build-time LDFLAGS into the pkgconfig files:
  # https://bugs.archlinux.org/task/68523
  patch -Np1 -d $_pkgname-${pkgver/_/-} -i ../$_pkgname-6.3-pkgconfig.patch
  # NOTE: can't run autoreconf because the autotools setup is custom and ancient

  patch -Np1 -d $_pkgname-${pkgver/_/-} -i ../mos.patch

  sed -i 's/| fiwix*/| fiwix* | mos*/' $_pkgname-${pkgver/_/-}/config.sub
}

build() {
  local configure_options=(
    --prefix=$_prefix
    --host=$_target
    --disable-root-access
    --disable-root-environ
    --disable-setuid-environ
    --enable-widec
    --enable-pc-files
    --mandir=$_prefix/share/man
    --with-cxx-binding
    --with-cxx-shared
    --with-manpage-format=normal
    --with-pkg-config-libdir=$_prefix/lib/pkgconfig
    --with-shared
    --with-versioned-syms
    --with-xterm-kbs=del
    --without-ada

  )

  cd $_pkgname-${pkgver/_/-}
  ./configure "${configure_options[@]}"
  make
}

package() {
  local _pkgver=${pkgver/_*/}

  make DESTDIR="$pkgdir" install -C $_pkgname-${pkgver/_/-}
  install -vDm 644 $_pkgname-${pkgver/_/-}/COPYING -t "$pkgdir$_prefix/share/licenses/$_pkgname/"

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    printf "INPUT(-l%sw)\n" "${lib}" > "$pkgdir$_prefix/lib/lib${lib}.so"
    ln -sv ${lib}w.pc "$pkgdir$_prefix/lib/pkgconfig/${lib}.pc"
  done

  # some packages look for -lcurses during build
  printf 'INPUT(-lncursesw)\n' > "$pkgdir$_prefix/lib/libcursesw.so"
  ln -sv libncurses.so "$pkgdir$_prefix/lib/libcurses.so"

  # tic and ticinfo functionality is built in by default
  # make sure that anything linking against it links against libncursesw.so instead
  for lib in tic tinfo; do
    printf "INPUT(libncursesw.so.%s)\n" "${_pkgver:0:1}" > "$pkgdir$_prefix/lib/lib${lib}.so"
    ln -sv libncursesw.so.${pkgver:0:1} "$pkgdir$_prefix/lib/lib${lib}.so.${_pkgver:0:1}"
    ln -sv ncursesw.pc "$pkgdir$_prefix/lib/pkgconfig/${lib}.pc"
  done
}
