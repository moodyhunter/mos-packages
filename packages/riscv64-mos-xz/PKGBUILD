# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

_target=riscv64-mos
_pkgbase=xz
pkgname=$_target-xz
pkgver=5.6.3
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('any')
url='https://xz.tukaani.org/xz-utils/'
license=('GPL' 'LGPL' 'custom')
depends=("$_target-bash" "$_target-mlibc")
makedepends=('git' 'po4a' 'doxygen')
provides=('liblzma.so')
options+=('!lto') # lto complains 'may be uninitialized `error_pos`'
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620') # Lasse Collin <lasse.collin@tukaani.org>
source=("git+https://git.tukaani.org/xz.git#tag=v${pkgver}")
sha256sums=('5a4495e85ddec6979a7c34fec7355304588e37830d663ffb5e57069636f47900')
sha512sums=('5dab9545bc63249c7716d40464f77b603a4255a91d65743d4fa8163e9819768c8a26b31dc783c1fb65904c954a6d5fd4a974420e4d599c7079828bdd3c78b593')

prepare() {
  cd $_pkgbase

  ./autogen.sh
  sed -i 's/| minix/| minix* | mos/' build-aux/config.sub
}

build() {
  cd $_pkgbase

  ./configure \
    --prefix=/opt/${_target} \
    --host=${_target} \
    --disable-rpath \
    --enable-werror
  make
}

package() {
  cd $_pkgbase

  make DESTDIR="${pkgdir}" install

  install -d -m0755 "${pkgdir}/opt/$_target/share/licenses/xz/"
  ln -sf /opt/$_target/share/doc/xz/COPYING "${pkgdir}/opt/$_target/share/licenses/xz/"
}
