_target=x86_64-elf
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.42
_basedir=$_pkgname-$pkgver
pkgrel=5
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=(x86_64)
url='https://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=(https://mirrors.kernel.org/gnu/binutils/binutils-$pkgver.tar.xz)
sha256sums=('f6e4d41fd5fc778b06b7891457b3620da5ecea1006c6a4a41ae998109f85a800')

prepare() {
    cd $_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --with-sysroot=/usr/$_target \
        --prefix="/usr" \
        --disable-nls \
        --disable-werror

    make
}

check() {
    cd $srcdir/$_pkgname-build
    # do not abort on errors - manually check log files
    make -k check
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install

    # Remove file conflicting with host binutils and manpages for MS Windows tools
    rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,windres,windmc}*
    rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so

    rm -r ${pkgdir}/usr/share/{info,man}
}
