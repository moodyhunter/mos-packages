_target=riscv64-mos
_sysroot=/opt/$_target

pkgname=$_target-gcc
pkgver=13.2.0
pkgrel=17
pkgdesc="GNU gcc for the $_target toolchain"
arch=('x86_64')
url="https://www.gnu.org/software/gcc"
license=('GPL')
provides=($pkgname)
groups=($_target-toolchain)
makedepends=(gmp mpfr gcc $_target-mlibc)
depends=(xz libmpc $_target-binutils)
options+=(!lto !debug staticlibs)
source=(
    "http://ftpmirror.gnu.org/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
    "mos-gcc.patch"
)
sha256sums=('e275e76442a6067341a27f04c5c6b83d8613144004c0413528863dc6b5c743da'
            'e5d90ec4c8ca0d7a5e9f2619f7f53600b079c4799e571d858fb7e0060984c2f8')

prepare() {
    # Patch gcc
    cd "gcc-$pkgver"
    patch -Np1 <"$srcdir/mos-gcc.patch"
}

build() {
    # Create temporary build dir
    mkdir -p "$_target-$pkgname-$pkgver-build"
    cd "$_target-$pkgname-$pkgver-build"

    # Credits @allanmcrae
    # https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/gcc/PKGBUILD
    # TODO: properly deal with the build issues resulting from this
    CFLAGS=${CFLAGS/-Werror=format-security/}
    CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

    # Configure, we are building in separate directory to cleanly separate the binaries from the source
    ../gcc-$pkgver/configure \
        --target=$_target \
        --prefix="/usr" \
        --libdir="/usr/lib" \
        --libexecdir="/usr/lib" \
        --disable-werror \
        --disable-nls \
        --disable-build-format-warnings \
        --enable-__cxa_atexit \
        --enable-linker-build-id \
        --enable-plugin \
        --enable-lto \
        --with-gnu-ld \
        --with-gnu-as \
        --with-sysroot="${_sysroot}" \
        --with-native-system-header-dir="/include" \
        --with-build-sysroot="${_sysroot}" \
        --enable-shared \
        --enable-threads=posix \
        --with-arch=rv64imafdc \
        --with-abi=lp64d

    make
}

package() {
    cd "$_target-$pkgname-$pkgver-build"

    # https://github.com/archlinux/svntogit-community/blob/packages/aarch64-linux-gnu-gcc/trunk/PKGBUILD
    make DESTDIR="$pkgdir" install-gcc install-target-{libgcc,libstdc++-v3,libgomp,libgfortran,libquadmath,libatomic}
    # strip target binaries
    find "$pkgdir"/usr/lib/gcc/$_target/ "$pkgdir"/usr/$_target/lib \
        -type f -and \( -name \*.a -or -name \*.o \) \
        -exec $_target-objcopy -R .comment -R .note -R .debug_info -R .debug_aranges \
        -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
        -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # strip host binaries
    find "$pkgdir"/usr/bin/ "$pkgdir"/usr/lib/gcc/$_target/ -type f -and \( -executable \) -exec strip '{}' \;

    # Remove files that conflict with host gcc package
    rm -r "$pkgdir"/usr/share/man/man7
    rm -r "$pkgdir"/usr/share/info
    rm -r "$pkgdir"/usr/share/gcc-$pkgver

    for so in libgcc_s.so libgcc_s.so.1 libstdc++.so; do
        install -Dm 755 ${pkgdir}/usr/riscv64-mos/lib/$so ${pkgdir}/${_sysroot}/lib/$so
    done
}
