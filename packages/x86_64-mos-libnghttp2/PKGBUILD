# Maintainer: Anatol Pomozov
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>

_target=x86_64-mos
pkgname=$_target-libnghttp2
pkgver=1.62.0
pkgrel=2
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=('any')
url='https://nghttp2.org/'
license=('MIT')
depends=($_target-mlibc)
provides=('libnghttp2.so')
makedepends=('git' "$_target-gcc")
conflicts=('nghttp2<1.20.0-2')
source=("git+https://github.com/nghttp2/nghttp2.git#tag=v${pkgver}"
        'git+https://github.com/ngtcp2/munit.git'
        'git+https://github.com/mruby/mruby.git'
        'git+https://github.com/tatsuhiro-t/neverbleed.git')
sha512sums=('4e775f93cd476660c3bab24820a7f4084dc6779d67894c613528f650cf3172028885c302d5cf22278b69c4431632235d78b88a5b82fdd359702251d386095697'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd nghttp2

  git config --file=.gitmodules submodule.tests/munit.url ../munit/
  git config --file=.gitmodules submodule.third-party/mruby.url ../mruby/
  git config --file=.gitmodules submodule.tests/neverbleed.url ../neverbleed/

  git submodule init
  git -c protocol.file.allow=always submodule update

  autoreconf -i
  sed -i 's/| minix/| minix* | mos/' config.sub
}

build() {
  cd nghttp2

  ./configure \
    --prefix=/opt/$_target \
    --host=$_target \
    --disable-examples \
    --disable-python-bindings \
    --enable-lib-only
  make
}

package() {
  cd nghttp2/lib

  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/opt/$_target/share/licenses/libnghttp2/COPYING"
}
