_target=x86_64-mos
_pkgname=binutils

pkgname=$_target-$_pkgname-hosted
pkgver=2.41
_basedir=$_pkgname-$pkgver
pkgrel=21
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf)
provides=($pkgname)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=($_target-binutils.patch
    https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz)
sha256sums=('089201e9f4786223ad7de7f07cbfb16cbbc2dba89831b25ef0f240bedd4dc2ae'
            'ae9a5789e23459e59606e6714723f2d3ffc31c03174191ef0d015bdf06007450')

prepare() {
    cd $srcdir/$_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    patch -Np1 <$srcdir/$_target-binutils.patch
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --prefix="/opt/$_target" \
        --with-build-sysroot=/opt/$_target \
        --disable-nls \
        --with-pic \
        --enable-deterministic-archives \
        --disable-werror \
        --enable-gold \
        --enable-ld=default \
        --enable-thread \
        --enable-shared \
        --enable-multilib \
        --enable-plugins \
        --with-gnu-as \
        --with-gnu-ld \
        --host=$_target \
        --build=${CHOST} \
        --enable-new-dtags

    make
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install
    rm -r "$pkgdir"/opt/$_target/share/info
}
