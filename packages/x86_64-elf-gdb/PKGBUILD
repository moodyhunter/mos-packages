_target=x86_64-elf

pkgname=$_target-gdb
pkgver=15.1
pkgrel=2
pkgdesc="GNU debugger crosscompiled for $_target development"
arch=(x86_64)
url="https://www.gnu.org/software/gdb"
license=('GPL')
groups=($_target-toolchain)
makedepends=(gmp mpfr)
depends=(xz ncurses expat python guile gdb-common mpfr libelf source-highlight)
provides=($pkgname)
source=("http://ftpmirror.gnu.org/gdb/gdb-$pkgver.tar.xz")
sha256sums=('38254eacd4572134bca9c5a5aa4d4ca564cbbd30c369d881f733fb6b903354f2')

build() {
    mkdir -p "$_target-gdb-$pkgver-build"
    cd "$_target-gdb-$pkgver-build"

    # Configure, we are building in separate directory to cleanly separate the binaries from the source
    ../gdb-$pkgver/configure \
        --target=$_target \
        --prefix=/usr \
        --program-prefix=$_target-

    make
}

package() {
    cd "$_target-gdb-$pkgver-build"
    make install DESTDIR=$pkgdir
    # Remove conflicting files
    rm -rf $pkgdir/usr/share/locale/
    rm -rf $pkgdir/usr/share/gdb
    rm -rf $pkgdir/usr/include/gdb
    rm -rf $pkgdir/usr/share/info/dir
    rm -rf $pkgdir/usr/share/info/
    rm -rf $pkgdir/usr/include/sim/callback.h
    rm -rf $pkgdir/usr/include/sim/sim.h
}
