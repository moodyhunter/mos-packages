# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

_target=riscv64-mos
_prefix=/opt/$_target

_pkgname=abseil-cpp
pkgname=$_target-abseil-cpp
pkgver=20240722.1
pkgrel=11
pkgdesc="Collection of C++ library code designed to augment the C++ standard library"
arch=(any)
url='https://abseil.io'
license=('Apache')
depends=("$_target-mlibc")
makedepends=('cmake' "$_target-gcc" 'mos-sdk' "$_target-gtest")
source=("https://github.com/abseil/abseil-cpp/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
         scoped-mock-log.patch
         mos-abseil-cpp.patch)
sha256sums=('40cee67604060a7c8794d931538cb55f4d444073e556980c88b6c49bb9b19bb7'
            'a6cbc612a2b96fcbd52d081e03e8581107ceb4827edb19d96510a31c568e1396'
            'f868ad3846cf9ee62de53e6dd6183d8d7708421c71dba283f00d333558ec1171')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  # patch -p1 -i ../scoped-mock-log.patch # Install target needed by protobuf
  patch -p1 -i ../mos-abseil-cpp.patch
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  cmake -Bbuild \
    -DCMAKE_TOOLCHAIN_FILE=/opt/${_target}/toolchain.cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DCMAKE_INSTALL_PREFIX=$_prefix \
    -DCMAKE_CXX_STANDARD=17 \
    -DBUILD_SHARED_LIBS=ON \
    -DABSL_BUILD_TEST_HELPERS=ON \
    -DABSL_USE_EXTERNAL_GOOGLETEST=ON \
    -DABSL_FIND_GOOGLETEST=ON \
    -DABSL_BUILD_TESTING=OFF  # Disable tests for now
  cd build
  cmake --build .
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"
  DESTDIR="$pkgdir" cmake --install .
}
