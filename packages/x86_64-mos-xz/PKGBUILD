# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

_target=x86_64-mos
_pkgbase=xz
pkgname=$_target-xz
pkgver=5.6.1
pkgrel=3
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('any')
url='https://xz.tukaani.org/xz-utils/'
license=('GPL' 'LGPL' 'custom')
depends=("$_target-bash" "$_target-mlibc")
makedepends=('git' 'po4a' 'doxygen')
provides=('liblzma.so')
options+=('!lto') # lto complains 'may be uninitialized `error_pos`'
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620') # Lasse Collin <lasse.collin@tukaani.org>
source=("git+https://git.tukaani.org/xz.git#tag=v${pkgver}")
sha256sums=('e10fa4254d5ff033c78dcbfd2866e79a762b8a719503a7c146758e590de945dc')
sha512sums=('8f4ee2e5c9b46d0917d8bdf8b172a70d02a6cf2d4d78a2e99ae942e32979b72b407809ffda2885af41e2c9d801c19eab5e4fd73888fbaf042346be957df406fc')

prepare() {
  cd $_pkgbase

  ./autogen.sh
  sed -i 's/| minix/| minix* | mos/' build-aux/config.sub
}

build() {
  cd $_pkgbase

  ./configure \
    --prefix=/opt/${_target} \
    --host=${_target} \
    --disable-rpath \
    --enable-werror
  make
}

package() {
  cd $_pkgbase

  make DESTDIR="${pkgdir}" install

  install -d -m0755 "${pkgdir}/opt/$_target/share/licenses/xz/"
  ln -sf /opt/$_target/share/doc/xz/COPYING "${pkgdir}/opt/$_target/share/licenses/xz/"
}
