# Maintainer: Pierre Schmitz <pierre@archlinux.de>
_target=x86_64-mos
_target_1=mos-x86_64
pkgname=$_target-openssl
pkgver=3.3.0
pkgrel=3
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=(any)
url='https://www.openssl.org'
license=('Apache-2.0')
depends=("$_target-mlibc")
makedepends=('perl' "$_target-gcc")
optdepends=("$_target-ca-certificates" "$_target-perl")
source=("https://www.openssl.org/source/openssl-${pkgver}.tar.gz"
        'ca-dir.patch'
		10-mos-main.conf.patch)
sha256sums=('53e66b043322a606abf0087e7699a0e033a37fa13feb9742df35c3a33b18fb02'
            '0a32d9ca68e8d985ce0bfef6a4c20b46675e06178cc2d0bf6d91bd6865d648b7'
            '3b5f1dbb644d66125cafc6321129cd8cb0ac896ca9c9076d7a93607612d963c4')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491'
              '7953AC1FBC3DC8B3B292393ED5E9E43F7DF9EE8C'
              'A21FAB74B0088AA361152586B8EF1A6BA9DA2D5C'
              'EFC0A467D613CB83C7ED6D30D894E2CE8B3D79F5')

prepare() {
	cd "$srcdir/openssl-$pkgver"

	# set ca dir to /etc/ssl by default
	patch -Np1 -i "$srcdir/ca-dir.patch"

	patch -Np1 -i "$srcdir/10-mos-main.conf.patch"
}

build() {
	cd "$srcdir/openssl-$pkgver"

	./Configure --prefix=/opt/$_target --libdir=lib \
		shared enable-ktls enable-ec_nistp_64_gcc_128 no-tests $_target_1

	make depend
	make
}

# check() {
# 	cd "$srcdir/openssl-$pkgver"

# 	# the test fails due to missing write permissions in /etc/ssl
# 	# revert this patch for make test
# 	patch -Rp1 -i "$srcdir/ca-dir.patch"

# 	make HARNESS_JOBS=$(nproc) test

# 	patch -Np1 -i "$srcdir/ca-dir.patch"
# 	# re-run make to re-generate CA.pl from the patched .in file.
# 	make apps/CA.pl
# }

package() {
	cd "$srcdir/openssl-$pkgver"

	make DESTDIR="$pkgdir" install_sw install_ssldirs

	install -D -m644 LICENSE.txt "$pkgdir/opt/$_target/share/licenses/openssl/LICENSE.txt"
}
