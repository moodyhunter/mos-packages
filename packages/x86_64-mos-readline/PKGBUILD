# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_target=x86_64-mos
_prefix=/opt/$_target

_pkgname=readline
pkgname=$_target-readline
_basever=8.2
_patchlevel=001
pkgver=${_basever}.${_patchlevel}
pkgrel=12
pkgdesc='GNU readline library'
arch=('x86_64')
url='https://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL')
backup=("opt/$_target/etc/inputrc")
depends=($_target-mlibc $_target-ncurses 'libncursesw.so')
makedepends=($_target-gcc)
provides=('libhistory.so' 'libreadline.so')
options=('!emptydirs' !strip debug)
source=(https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz
        inputrc)
sha256sums=('3feb7171f16a84ee82ca18a36d7b9be109a52c04f492a053331d7d1095007c35'
            '36e9611f935ee108d161587b0615f9c390192ef4bbff6dc59b58671261029901'
            'bbf97f1ec40a929edab5aa81998c1e2ef435436c597754916e6a5868f273aff7')

if [ $_patchlevel -gt 0 ]; then
    for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p))
    done
fi

prepare() {
  cd $_pkgname-$_basever
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  # remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  sed -i 's/| fiwix*/| fiwix* | mos*/' support/config.sub
}

build() {
  cd $_pkgname-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=$_prefix --host=$_target
  make SHLIB_LIBS=-lncurses
}

package() {
  make -C $_pkgname-$_basever DESTDIR="$pkgdir" install
  install -Dm644 inputrc "$pkgdir"$_prefix/etc/inputrc
  rm "$pkgdir"$_prefix/share/info/dir || true
}
