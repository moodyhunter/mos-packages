# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

_target=riscv64-elf
pkgname=$_target-gcc
pkgver=14.2.0
pkgrel=1
pkgdesc='The GNU Compiler Collection - cross compiler for RISCV64 target - bootstrap version. only used to build initial glibc and compiler'
arch=(x86_64)
url='http://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=($_target-binutils libmpc zlib)
makedepends=(gmp mpfr)
options=(!emptydirs !strip !lto)
groups=($_target-toolchain)
source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz)
sha512sums=('932bdef0cda94bacedf452ab17f103c0cb511ff2cec55e9112fc0328cbf1d803b42595728ea7b200e0a057c03e85626f937012e49a7515bc5dd256b2bf4bc396')

build() {
    mkdir -p "$_target-$pkgname-$pkgver-build"
    cd "$_target-$pkgname-$pkgver-build"

    # Credits @allanmcrae
    # https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/gcc/PKGBUILD
    # TODO: properly deal with the build issues resulting from this
    CFLAGS=${CFLAGS/-Werror=format-security/}
    CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

    ../gcc-$pkgver/configure \
        --target=$_target \
        --prefix="/usr" \
        --libexecdir="/usr/lib" \
        --disable-nls \
        --disable-werror \
        --disable-multilib \
        --without-headers \
        --enable-libgcc \
        --enable-languages=c,c++ \
        --with-arch=rv64imafdc \
        --with-abi=lp64d \
        --disable-build-format-warnings # https://bugs.archlinux.org/task/70701

    make all-gcc
    make all-target-libgcc
}

package() {
    cd "$_target-$pkgname-$pkgver-build"

    make DESTDIR="$pkgdir" install-gcc
    make DESTDIR="$pkgdir" install-target-libgcc

    # Remove conflicting files
    rm -rf $pkgdir/usr/share/info
    rm -rf $pkgdir/usr/share/man/man7
}
