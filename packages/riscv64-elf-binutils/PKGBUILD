_target=riscv64-elf
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.42
_basedir=$_pkgname-$pkgver
pkgrel=5
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=('x86_64')
url='https://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib)
groups=($_target-toolchain)
source=("https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.bz2")
sha512sums=('d2c6d264bbeaaaf2aad1fa3fc417c1841a5dd4a299fa47c47d15adb821c22dae733e58f19ebcfea3b0c5890ba56e4a5f92ab55128a572d585bcd5172b63f456c')

prepare() {
    cd $_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --prefix=/usr \
        --enable-multilib \
        --enable-interwork \
        --with-gnu-as \
        --with-gnu-ld \
        --disable-nls \
        --enable-ld=default \
        --enable-gold \
        --enable-plugins \
        --enable-deterministic-archives

    make
}

check() {
    cd $srcdir/$_pkgname-build
    # do not abort on errors - manually check log files
    make -k check
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install

    # Remove file conflicting with host binutils and manpages for MS Windows tools
    rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,windres,windmc}*
    rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so

    rm -r ${pkgdir}/usr/share/{info,man}
}
