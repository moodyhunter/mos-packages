# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_target=x86_64-mos
_pkgbase=curl
pkgname=$_target-curl
pkgver=8.8.0
pkgrel=7
pkgdesc='command line tool and library for transferring data with URLs'
arch=('any')
url='https://curl.se/'
license=('MIT')
depends=(
  # "$_target-ca-certificates"
  "$_target-brotli"
  # "$_target-krb5"
  # "$_target-libidn2"
  "$_target-libnghttp2"
  "$_target-libnghttp3"
  # "$_target-libpsl"
  # "$_target-libssh2"
  "$_target-openssl"
  "$_target-zlib"
  # "$_target-zstd"
)
makedepends=('git' 'patchelf' "$_target-gcc")
checkdepends=('valgrind')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
source=("git+https://github.com/curl/curl.git#tag=curl-${pkgver//./_}")
sha512sums=('743cac6059697b8aec637cb0ca94bc0606a9bbcf274f77847a1ceb9bbadc0259a9fbf30133323af2543d8df65ec6cd3e396cd999aca8fe73dde794a9c3dcbf6c')

_backports=(
)

_reverts=(
)

prepare() {
  cd "$_pkgbase"

  local _c
  for _c in "${_backports[@]}"; do
    if [[ $_c == *..* ]]; then
      git log --oneline --reverse "${_c}"
    else
      git log --oneline -1 "${_c}"
    fi
    git cherry-pick -n -m1 "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done

  # no '-DEV' in version, release date from tagged commit...
  sed -i \
    -e "/\WLIBCURL_VERSION\W/c #define LIBCURL_VERSION \"${pkgver}\"" \
    -e "/\WLIBCURL_TIMESTAMP\W/c #define LIBCURL_TIMESTAMP \"$(git log -1 --format=%cs "curl-${pkgver//./_}")\"" \
    include/curl/curlver.h

  autoreconf -fi
  sed -i 's/| minix/| minix* | mos/' config.sub
}

build() {
  local _configure_options=(
    --host="${_target}"
    --prefix="/opt/$_target"
    --mandir="/opt/$_target/share/man"
    --disable-ldap
    --disable-ldaps
    --disable-manual
    --enable-ipv6
    --enable-threaded-resolver
    --with-gssapi
    # --with-libssh2
    --with-random='/dev/urandom'
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'
  )

  mkdir build-curl{,-compat,-gnutls}

  # build curl
  cd "${srcdir}"/build-curl

  "${srcdir}/${_pkgbase}"/configure \
    "${_configure_options[@]}" \
    --enable-versioned-symbols \
    --with-fish-functions-dir=/opt/$_target/share/fish/vendor_completions.d/ \
    --with-openssl \
    --with-openssl-quic \
    --with-zsh-functions-dir=/opt/$_target/share/zsh/site-functions/
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=('openssl' 'libcrypto.so' 'libssl.so')
  provides=('libcurl.so')

  cd build-curl

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install -C scripts

  cd "${srcdir}/${_pkgbase}"

  # license
  install -Dt "${pkgdir}/opt/$_target/share/licenses/${_pkgbase}" -m0644 COPYING
}
