# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

_target=x86_64-mos
_prefix=/opt/$_target

_pkgname=abseil-cpp
pkgname=$_target-abseil-cpp
pkgver=20230802.1
pkgrel=1
pkgdesc="Collection of C++ library code designed to augment the C++ standard library"
arch=('x86_64')
url='https://abseil.io'
license=('Apache')
depends=("$_target-mlibc")
makedepends=('cmake' "$_target-gcc" "mos-api-headers-x86_64-git" 'mos-sdk' "$_target-gtest")
source=("https://github.com/abseil/abseil-cpp/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
         scoped-mock-log.patch
         mos-abseil-cpp.patch
         $_target.cmake)
sha256sums=('987ce98f02eefbaf930d6e38ab16aa05737234d7afbab2d5c4ea7adbe50c28ed'
            'a6cbc612a2b96fcbd52d081e03e8581107ceb4827edb19d96510a31c568e1396'
            'e709de097bf1c76b9925372e2f81c680a71b16bf3e43d205e7ac091ef55dabba'
            'd4ef294a1c197789d7e4affa9a61e6d8827a9060c8bfe3065d70ac9f35aa4af0')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  # patch -p1 -i ../scoped-mock-log.patch # Install target needed by protobuf
  patch -p1 -i ../mos-abseil-cpp.patch
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  cmake -Bbuild \
    -DCMAKE_TOOLCHAIN_FILE=../$_target.cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DCMAKE_INSTALL_PREFIX=$_prefix \
    -DCMAKE_CXX_STANDARD=17 \
    -DBUILD_SHARED_LIBS=ON \
    -DABSL_BUILD_TEST_HELPERS=ON \
    -DABSL_USE_EXTERNAL_GOOGLETEST=ON \
    -DABSL_FIND_GOOGLETEST=ON
  cd build
  cmake --build .
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"
  DESTDIR="$pkgdir" cmake --install .
}
