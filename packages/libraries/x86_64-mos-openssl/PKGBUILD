# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=x86_64-mos-openssl
pkgver=3.2.1
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('Apache-2.0')
depends=('x86_64-mos-mlibc')
makedepends=('perl')
optdepends=('x86_64-mos-ca-certificates' 'x86_64-mos-perl')
replaces=('openssl-perl' 'openssl-doc')
provides=('libcrypto.so' 'libssl.so')
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/openssl-${pkgver}.tar.gz"
        'ca-dir.patch'
		'10-main.conf.patch')
sha256sums=('83c7329fe52c850677d75e5d0b0ca245309b97e8ecbcfdc1dfdc4ab9fac35b39'
            '0a32d9ca68e8d985ce0bfef6a4c20b46675e06178cc2d0bf6d91bd6865d648b7'
            'ab0cc43dfbf27c92ffdd933673d127c0a603ac46e910b3f673170c6f1bb02c83')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491'
              '7953AC1FBC3DC8B3B292393ED5E9E43F7DF9EE8C'
              'A21FAB74B0088AA361152586B8EF1A6BA9DA2D5C'
              'EFC0A467D613CB83C7ED6D30D894E2CE8B3D79F5')

prepare() {
	cd "$srcdir/openssl-$pkgver"

	# set ca dir to /etc/ssl by default
	patch -Np1 -i "$srcdir/ca-dir.patch"

	patch -Np1 -i "$srcdir/10-main.conf.patch"
}

build() {
	cd "$srcdir/openssl-$pkgver"

	./Configure --prefix=/opt/x86_64-mos --libdir=lib \
		shared enable-ktls enable-ec_nistp_64_gcc_128 mos-x86_64

	make depend
	make
}

# check() {
# 	cd "$srcdir/openssl-$pkgver"

# 	# the test fails due to missing write permissions in /etc/ssl
# 	# revert this patch for make test
# 	patch -Rp1 -i "$srcdir/ca-dir.patch"

# 	make HARNESS_JOBS=$(nproc) test

# 	patch -Np1 -i "$srcdir/ca-dir.patch"
# 	# re-run make to re-generate CA.pl from the patched .in file.
# 	make apps/CA.pl
# }

package() {
	cd "$srcdir/openssl-$pkgver"

	make DESTDIR="$pkgdir" install_sw install_ssldirs

	install -D -m644 LICENSE.txt "$pkgdir/opt/x86_64-mos/share/licenses/openssl/LICENSE.txt"
}
