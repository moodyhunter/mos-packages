# Maintainer: Moody <mooodyhunter@outlook.com>

_arch=x86_64
_target=x86_64-mos
_sysroot=/opt/${_target}
_pkgbase=mos-api-headers-${_arch}

pkgname=${_pkgbase}-git
pkgver=1666.ce08ae02a9a6
pkgrel=1
pkgdesc="MOS API headers for ${_arch}"
arch=('any')
url="https://github.com/moodyhunter/MOS"
license=('GPL3')
provides=($_pkgbase)
conflicts=($_pkgbase)
groups=(mos-toolchain)
makedepends=(git cmake python mos-sdk)
source=("mos::git+https://github.com/moodyhunter/MOS.git")
sha512sums=('SKIP')

pkgver() {
    cd mos
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd mos
    git submodule update --init --recursive
}

build() {
    cd mos
    mkdir -p build && cd build
    cmake .. \
        -DMOS_ARCH=${_arch} \
        -D__MOS_HAS_NO_COMPILER=OFF \
        -DCMAKE_INSTALL_PREFIX=${pkgdir}$_sysroot

    cmake --build . --target mos_syscall_decl
}

package() {
    cd mos/build
    cmake --install . --component mos-api-headers
}
