# Maintainer: Anatol Pomozov
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>

_target=riscv64-mos
pkgname=$_target-libnghttp2
pkgver=1.65.0
pkgrel=3
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=('any')
url='https://nghttp2.org/'
license=('MIT')
depends=($_target-mlibc)
provides=('libnghttp2.so')
makedepends=('git' "$_target-gcc")
conflicts=('nghttp2<1.20.0-2')
source=("git+https://github.com/nghttp2/nghttp2.git#tag=v${pkgver}"
        'git+https://github.com/ngtcp2/munit.git'
        'git+https://github.com/mruby/mruby.git'
        'git+https://github.com/tatsuhiro-t/neverbleed.git')
sha512sums=('d82375c360ac385f47bc94eb730b99a595c3b16c3b41f51594f005e3d93edc05779660fbb7da304998507b7eeb4c9a600520e047e1d23ee591f86b62ac8cd781'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd nghttp2

  git config --file=.gitmodules submodule.tests/munit.url ../munit/
  git config --file=.gitmodules submodule.third-party/mruby.url ../mruby/
  git config --file=.gitmodules submodule.tests/neverbleed.url ../neverbleed/

  git submodule init
  git -c protocol.file.allow=always submodule update

  autoreconf -i
  sed -i 's/| minix/| minix* | mos/' config.sub
}

build() {
  cd nghttp2

  ./configure \
    --prefix=/opt/$_target \
    --host=$_target \
    --disable-examples \
    --disable-python-bindings \
    --enable-lib-only
  make
}

package() {
  cd nghttp2/lib

  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/opt/$_target/share/licenses/libnghttp2/COPYING"
}
